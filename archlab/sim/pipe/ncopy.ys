#/* $begin ncopy-ys */
##################################################################
# ncopy.ys - Copy a src block of len words to dst.
# Return the number of positive words (>0) contained in src.
#
##################################################################
# Do not modify this portion
# Function prologue.
# %rdi = src, %rsi = dst, %rdx = len
ncopy:
##################################################################
# You can modify this portion
	# Loop header
	xorq %rax,%rax		
	andq %rdx,%rdx		
	jle Done		
Loop:	iaddq $-6, %rdx
	andq %rdx, %rdx
	jge L1
	iaddq $1, %rdx
	andq %rdx, %rdx
	je Five_left
	iaddq $1, %rdx
	andq %rdx, %rdx
	je Four_left
	iaddq $1, %rdx
	andq %rdx, %rdx
	je Three_left
	iaddq $1, %rdx
	andq %rdx, %rdx
	je Two_left
	iaddq $1, %rdx
	mrmovq (%rdi), %r13
	rmmovq %r13, (%rsi)
	iaddq $8, %rdi
	iaddq $8, %rsi
	jmp L6
Five_left:	mrmovq (%rdi), %r9
		mrmovq $8(%rdi), %r10
		mrmovq $16(%rdi), %r11
		mrmovq $24(%rdi), %r12
		mrmovq $32(%rdi), %r13
		rmmovq %r9, (%rsi)
		rmmovq %r10, $8(%rsi)
		rmmovq %r11, $16(%rsi)
		rmmovq %r12, $24(%rsi)
		rmmovq %r13, $32(%rsi)
		iaddq $40, %rdi
		iaddq $40, %rsi
		jmp L2
Four_left:	mrmovq (%rdi), %r10
		mrmovq $8(%rdi), %r11
		mrmovq $16(%rdi), %r12
		mrmovq $24(%rdi), %r13
		rmmovq %r10, (%rsi)
		rmmovq %r11, $8(%rsi)
		rmmovq %r12, $16(%rsi)
		rmmovq %r13, $24(%rsi)
		iaddq $32, %rdi
		iaddq $32, %rsi
		jmp L3
Three_left:	mrmovq (%rdi), %r11
		mrmovq $8(%rdi), %r12
		mrmovq $16(%rdi), %r13
		rmmovq %r11, (%rsi)
		rmmovq %r12, $8(%rsi)
		rmmovq %r13, $16(%rsi)
		iaddq $24, %rdi
		iaddq $24, %rsi
		jmp L4
Two_left:	mrmovq (%rdi), %r12
		mrmovq $8(%rdi), %r13
		rmmovq %r12, (%rsi)
		rmmovq %r13, $8(%rsi)
		iaddq $16, %rdi
		iaddq $16, %rsi
		jmp L5
L1:	mrmovq (%rdi), %r8
	mrmovq $8(%rdi), %r9
	mrmovq $16(%rdi), %r10
	mrmovq $24(%rdi), %r11
	mrmovq $32(%rdi), %r12
	mrmovq $40(%rdi), %r13
	rmmovq %r8, (%rsi)
	rmmovq %r9, $8(%rsi)
	rmmovq %r10, $16(%rsi)
	rmmovq %r11, $24(%rsi)
	rmmovq %r12, $32(%rsi)
	rmmovq %r13, $40(%rsi)
	iaddq $48, %rdi
	iaddq $48, %rsi
	andq %r8, %r8
	jle L2
	iaddq $1, %rax
L2:	andq %r9, %r9
	jle L3
	iaddq $1, %rax
L3:	andq %r10, %r10
	jle L4
	iaddq $1, %rax
L4:	andq %r11, %r11
	jle L5
	iaddq $1, %rax
L5:	andq %r12, %r12
	jle L6
	iaddq $1, %rax
L6:	andq %r13, %r13
	jle Npos
	iaddq $1, %rax
Npos:	andq %rdx, %rdx
	jg Loop
##################################################################
# Do not modify the following section of code
# Function epilogue.
Done:
	ret
##################################################################
# Keep the following label at the end of your function
End:
#/* $end ncopy-ys */
